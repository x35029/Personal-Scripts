<Package>
	<Job id="CtxTotalFarmSessions">
		<Comment>
			File: CtxTotalFarmSessions.wsf
			Author:	Kleber Carraro
			Change History: 03/25/2013 - Script Created.
			
		</comment>
		<runtime>
			<description>
				Enum Total Sessions per Farm.
			</description>
			<example>
				CScript //nologo CtxTotalFarmSessions.wsf
			</example>
  		</runtime>
		<reference object="MetaFrameCOM.MetaFrameFarm"/>
		<script language="VBScript">
		Option Explicit

		'
		' Debug Handler - Ref: http://technet.microsoft.com/en-us/library/ee156618.aspx
		'
		Public DEBUGMODE
		If WScript.Arguments.Count = 1 Then
			If WScript.Arguments.Item(0) = "" OR UCASE(WScript.Arguments.Item(0)) <> "DEBUG" Then
				DEBUGMODE = False
			ElseIf UCase(WScript.Arguments.Item(0)) = "DEBUG" Then
				WScript.Arguments.Item(0) 
				DEBUGMODE = True
			End If
		Else
			DEBUGMODE = False
		End If
		WScript.StdOut.WriteLine("Debug Mode: " & DEBUGMODE)
		If DEBUGMODE = False Then
			On Error Resume Next
		Else
			On Error Goto 0
		End If
		
		'
		' Environment Setup
		'
		Const FOR_APPENDING = 8
		Dim strScriptPath 	: strScriptPath = Replace(WScript.ScriptFullName, WScript.ScriptName, "")
		Dim arrScriptName 	: arrScriptName = Split(WScript.ScriptName, ".", -1)
		Dim strScriptName 	: strScriptName = arrScriptName(LBound(arrScriptName))  
		Dim objFSO		: Set objFSO = CreateObject( "Scripting.FileSystemObject" )
		Dim objShell	  	: Set objShell = WScript.CreateObject("WScript.Shell")
		Dim objNetwork		: Set objNetwork = CreateObject("Wscript.Network")
		Dim objNamedArguments	: Set objNamedArguments = WScript.Arguments.Named
		Dim strCurrentDate	: strCurrentDate =  Month(Date) & "-" & Day(Date) & "-" & Year(Date) & "_" & Replace(Left(Time,8),":","-")
		Dim strLogFile		: strLogFile = FnCreateLog(strScriptName, strScriptPath)
		Dim objLogFile		: Set objLogFile = objFSO.OpenTextFile(strLogFile, FOR_APPENDING)
		Dim strHeadings, strResults
		Public bIsLogFileEmpty
				
		' 
		' Functional Variables
		'
		Dim theFarm
		
		'
		' Create MetaFrameFarm object
		'
		Set theFarm = CreateObject("MetaFrameCOM.MetaFrameFarm")
		If Err.Number <> 0 Then
			WScript.StdOut.WriteLine "Can't create MetaFrameFarm object."
			WScript.StdOut.WriteLine "(" & Err.Number & ") " & Err.Description
			WScript.StdOut.WriteBlankLines(1)
			Call ObjectCleanup()			
		Else
			Dim strFarmName : strFarmName = theFarm.FarmName
		End If
		
		'
		' Initialize the farm object.
		'
		theFarm.Initialize(MetaFrameWinFarmObject)
		If Err.Number <> 0 Then
			WScript.StdOut.WriteLine "Can't  Initialize MetaFrameFarm object."
			WScript.StdOut.WriteLine "(" & Err.Number & ") " & Err.Description
			WScript.StdOut.WriteBlankLines(1)
			Call ObjectCleanup()
		End If

		'
		' Are you Citrix Administrator?
		'
		If theFarm.WinFarmObject.IsCitrixAdministrator = 0 then
			WScript.StdOut.WriteLine "You must be a Citrix admin to run this script." 
			WScript.StdOut.WriteBlankLines(1)
			Call ObjectCleanup()
		End If

		'
		' Farm Total Sessions
		'

		' Output Headings
		strHeadings =	"Farm" & "," & _
				"Session Count" & "," & _
				"Year"  & "," & _
				"Month"  & "," & _
				"Day"  & "," & _
				"Time"

		' Log File Headings
		If bIsLogFileEmpty = True Then
			'Removed headings to facilitate the transport to Data.xlsb
			objLogFile.WriteLine strHeadings
		End If
		
		'Get Total Sessions
		Dim intFarmSessionCount : intFarmSessionCount = theFarm.Sessions.Count
		'Get Date and Time
		Dim dtmDateTime : dtmDateTime = CDate(Now())
		Dim dYear	: dYear = Year(dtmDateTime)
		Dim strMonth	: strMonth = GetMonthName(Month(dtmDateTime))
		Dim dDay	: dDay = Day(dtmDateTime)
		Dim tHour	: tHour = Hour(dtmDateTime)

		'Set Results
		strResults = strFarmName  & "," & _
				intFarmSessionCount & "," & _
				dYear & "," & _
				strMonth & "," & _
				dDay & "," & _
				tHour

		objLogFile.WriteLine strResults
		Call FnShowResults(strResults)
		strResults = ""

'		Wscript.StdOut.WriteLine("Farm Total Sessions (" & theFarm.FarmName & "): " & theFarm.Sessions.Count)

		Call ObjectCleanup()
		WScript.StdOut.WriteBlankLines(1)
 		WScript.StdOut.WriteLine("Log File Created: " & strLogFile)

		' 
		' Function Object Cleanup - Clean / Delete objects created during the execution.
		'
		Function ObjectCleanup()
			Set theFarm = Nothing
			objLogFile.Close
			Set objNamedArguments = Nothing
			Set objShell = Nothing
			Set objFSO = Nothing
			Set objNetwork = Nothing
			Wscript.Quit
		End Function

		'
		'Function GetMonthName(iMonth) - Convert integer month into string
		'
		Function GetMonthName(iMonth)
			Select Case iMonth
				Case 1  GetMonthName = "January"
				Case 2  GetMonthName = "February"
				Case 3  GetMonthName = "March"
				Case 4  GetMonthName = "April"
				Case 5  GetMonthName = "May"
				Case 6  GetMonthName = "June"
				Case 7  GetMonthName = "July"		
				Case 8  GetMonthName = "August"		
				Case 9  GetMonthName = "September"		
				Case 10 GetMonthName = "October"		
				Case 11 GetMonthName = "November"		
				Case 12 GetMonthName = "December"		
			End Select
		End Function

		'
		' Function Create Output Log File (scriptname)
		'
		Function FnCreateLog(strScriptName, strScriptPath) 
			Const FOR_READING = 1 
			Dim strLogPath	: strLogPath = strScriptPath & "Output\"
			Dim strLogName	: strLogName = strScriptName & ".txt"
			Dim strMessage
			If Not objFSO.FolderExists(strLogPath) Then
				objFSO.CreateFolder(strLogPath)
			End If
			If Not objFSO.FileExists(strLogPath & strLogName) Then
				objFSO.CreateTextFile strLogPath & strLogName
				bIsLogFileEmpty = True
			Else
				Set objLogFile = objFSO.OpenTextFile(strLogPath & strLogName, FOR_READING)
					If objLogFile.AtEndOfStream Then
						bIsLogFileEmpty = True
					Else
						bIsLogFileEmpty = False
					End If
				Set objLogFile = Nothing
			End If
			FnCreateLog = strLogPath & strLogName
		End Function

		'
		' Function Show Results on Screen
		'
		Function FnShowResults(strContents)
			WScript.StdOut.WriteLine strContents
		End Function
		</script>
	</job>
</package>